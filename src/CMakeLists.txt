PROJECT(PosteRazor)

IF (WIN32 OR APPLE)
	SET(POSTERAZOR_ENABLEGLPREVIEW TRUE)
	SET(POSTERAZOR_ENABLELAUNCHURLSORFILES TRUE)
ENDIF (WIN32 OR APPLE)

IF (EXISTS PosteRazor.cpp)
	IF (WIN32)
		MESSAGE(FATAL_ERROR "The flat source tree is not buildable on Windows. Please use the normal source package.") 
	ENDIF (WIN32)
	INCLUDE (CMakeLists_flat_source_tree.txt)
ELSE (EXISTS PosteRazor.cpp)
	INCLUDE (CMakeLists_deep_source_tree.txt)
ENDIF (EXISTS PosteRazor.cpp)

SET(POSTERAZOR_SOURCES
	${POSTERAZOR_CORESOURCES}
	${POSTERAZOR_UISOURCES}
)

SET(POSTERAZOR_DEFINITIONS
	-DFREEIMAGE_LIB
)

IF (NOT POSTERAZOR_ENABLEGLPREVIEW)
	SET(POSTERAZOR_DEFINITIONS
		${POSTERAZOR_DEFINITIONS}
		-DNO_OPENGL_PREVIEW
	)
ENDIF (NOT POSTERAZOR_ENABLEGLPREVIEW)

IF (NOT POSTERAZOR_ENABLELAUNCHURLSORFILES)
	SET(POSTERAZOR_DEFINITIONS
		${POSTERAZOR_DEFINITIONS}
		-DNO_LAUNCH_URLS_OR_FILES
	)
ENDIF (NOT POSTERAZOR_ENABLELAUNCHURLSORFILES)

IF (WIN32)
	SET(POSTERAZOR_EXECUTABLE WIN32 ${POSTERAZOR_SOURCES} ${POSTERAZOR_RC_FILE})
	SET(POSTERAZOR_INCLUDEDIRECTORIES ${POSTERAZOR_INCLUDEDIRECTORIES}
		../../../thirdparty/fltk-1.1
		../../../source/posterazor_ui_common/windowsResources
	)
	SET(POSTERAZOR_LINKDIRECTORIES
		../../../thirdparty/FreeImage/Dist
		../../../thirdparty/fltk-1.1/lib
	)
	SET(POSTERAZOR_LINKLIBRARIES
		debug FreeImaged
		optimized FreeImage
		debug fltkd
		optimized fltk
		wsock32
		comctl32
	)
	IF (POSTERAZOR_ENABLEGLPREVIEW)
		SET(POSTERAZOR_LINKLIBRARIES
			${POSTERAZOR_LINKLIBRARIES}
			debug fltkgld
			optimized fltkgl
			glu32
			opengl32
		)
	ENDIF (POSTERAZOR_ENABLEGLPREVIEW)
	SET(POSTERAZOR_TARGET_PROPERTIES PROPERTIES LINK_FLAGS "/nodefaultlib:LIBCMT /nodefaultlib:LIBCMTD")
ENDIF (WIN32)

IF (APPLE)
	SET(POSTERAZOR_EXECUTABLE MACOSX_BUNDLE ${POSTERAZOR_SOURCES})
	SET(POSTERAZOR_LINKLIBRARIES
		fltk
		fltk_gl
		freeimage
		"-framework Carbon -framework AGL -framework ApplicationServices -framework OpenGL -lz"
	)
ENDIF(APPLE)

IF (UNIX AND NOT APPLE)
	SET(POSTERAZOR_EXECUTABLE ${POSTERAZOR_SOURCES})
	SET(POSTERAZOR_LINKLIBRARIES
		fltk
		fltk_images
		freeimage
		X11
		Xpm
#		Xft
#		Xinerama
	)
	IF (POSTERAZOR_ENABLEGLPREVIEW)
		SET(POSTERAZOR_LINKLIBRARIES
			${POSTERAZOR_LINKLIBRARIES}
			fltk_gl
		)
	ENDIF (POSTERAZOR_ENABLEGLPREVIEW)
ENDIF (UNIX AND NOT APPLE)

LINK_DIRECTORIES(${POSTERAZOR_LINKDIRECTORIES})
ADD_EXECUTABLE(PosteRazor ${POSTERAZOR_EXECUTABLE})
IF (DEFINED POSTERAZOR_TARGET_PROPERTIES)
	SET_TARGET_PROPERTIES(PosteRazor ${POSTERAZOR_TARGET_PROPERTIES})
ENDIF (DEFINED POSTERAZOR_TARGET_PROPERTIES)
ADD_DEFINITIONS(${POSTERAZOR_DEFINITIONS})
INCLUDE_DIRECTORIES(${POSTERAZOR_INCLUDEDIRECTORIES})
TARGET_LINK_LIBRARIES(PosteRazor ${POSTERAZOR_LINKLIBRARIES})

IF (APPLE)
	GET_TARGET_PROPERTY(MACOSX_BUNDLE_LOCATION PosteRazor LOCATION)
	SET(MACOSX_CONTENTS_PATH ${MACOSX_BUNDLE_LOCATION}.app/Contents)
	SET(MACOSX_RESOURCES_PATH ${MACOSX_CONTENTS_PATH}/Resources)
	ADD_CUSTOM_COMMAND(TARGET PosteRazor POST_BUILD
		COMMAND mkdir ARGS -p ${MACOSX_RESOURCES_PATH}
		COMMAND cp ARGS ${POSTERAZOR_ICON_FILE} ${MACOSX_RESOURCES_PATH}
		COMMAND cp ARGS ${POSTERAZOR_INFOPLIST} ${MACOSX_CONTENTS_PATH}
		COMMAND strip ARGS ${MACOSX_CONTENTS_PATH}/MacOS/PosteRazor
		)
	FOREACH(language English German French Italian Dutch Spanish)
		ADD_CUSTOM_COMMAND(TARGET PosteRazor POST_BUILD
			COMMAND mkdir ARGS -p ${MACOSX_RESOURCES_PATH}/${language}.lproj)
	ENDFOREACH(language)
ENDIF(APPLE)
